;;
;; Animate by alternating drawing figures and "cs".
;;

to reset
    pu
    home
    cs
    pd
end

to arrow :heading :size
    rt :heading
    fd :size    ; shaft

    rt 150      ; arrowhead
    fd :size/10.0
    bk :size/10.0
    lt 300
    fd :size/10.0
    bk :size/10.0
    rt 150

    bk :size    ; return to initial position
    lt :heading
end

to animate_arrow :heading :size
    arrow :heading :size
    wait 1
    reset
    animate_arrow :heading+1 :size
end

;; Man doing jumping jacks
to man :heading :size
    setpencolor 14
    repeat 36 [fd :size/20 rt 10]   ; head

    lt 90
    fd :size/5.0                    ; neck

    rt 90                           ; right arm
    lt :heading
    fd :size
    bk :size
    rt :heading
    lt 90

    lt 90                           ; left arm
    rt :heading
    fd :size
    bk :size
    lt :heading
    rt 90

    fd :size                        ; body

    rt 45 + :heading/1.2            ; right leg
    fd :size
    bk :size
    lt 45 + :heading/1.2

    lt 45 + :heading/1.2            ; left leg
    fd :size
    bk :size
    rt 45 + :heading/1.2

    rt 180                          ; reset original position
    fd :size                        ; length of body
;   fd :size/5.0                    ; length of neck
    lt 90
end

to animate_man :heading :size :heading_sign
    lt 90
    man :heading :size
    wait 1
    reset
    ;; This is clumsy; I need to learn Logo's syntax for conditionals.
    if :heading = 40   [animate_man :heading + 4*-:heading_sign :size -:heading_sign                    stop]
    if :heading = -40  [animate_man :heading + 4*-:heading_sign :size -:heading_sign                    stop]
    animate_man :heading + 4*:heading_sign :size :heading_sign
end

ht
animate_man 0 100 1

;;animate_arrow 0 100